/*
 * Copyright 2019 Jonathan Paz <omahaprogrammer@gmail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.omahaprogrammer.crypto'
version '1.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_10

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all"
}

artifacts {
    archives javadocJar, sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.62'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

test {
    minHeapSize = "1024m"
    maxHeapSize = "2048m"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'PHC format library'
                description = 'A library designed to produce and read PHC-formatted strings and verify passwords against them'
                url = 'https://github.com/omahaprogrammer/phc-format'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'omahaprogrammer'
                        name = 'Jonathan Paz'
                        email = 'omahaprogrammer@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/omahaprogrammer/phc-format.git"
                    developerConnection = "scm:git:ssh://github.com:omahaprogrammer/phc-format.git"
                    url = 'https://github.com/omahaprogrammer/phc-format/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('-SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}
